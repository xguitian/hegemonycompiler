package cup;

import java_cup.runtime.*;
import java.util.List;
import java.util.ArrayList;
import hegemony.main.HegemonyOrder;
import hegemony.main.HegemonyOrder.ErrorType;
import hegemony.main.OrdersFile;
import hegemony.scanner.HegemonySymbol;
import hegemony.exception.ParserFatalErrorException;
import hegemony.main.ParserFatalError;

parser code  {:

  public void report_error(String message, Object info) {
//    StringBuffer m = new StringBuffer("Error ");

//    if (info instanceof java_cup.runtime.Symbol) 
//      m.append( "("+info.toString()+")" );
     
//    m.append(" : "+message);
   
//    System.out.println(m);
  }
   
  public void report_fatal_error(String message, Object info) 
  		throws ParserFatalErrorException {
    throw new ParserFatalErrorException(new ParserFatalError((HegemonySymbol) info));
  }
:};

terminal	EOL;
terminal	WORD, NUMBER;
terminal	ARMY, NAVY;
terminal	MOVE, SUPPORT, POLITIC_POINTS, DECLARE_WAR, DECLARE_PEACE;

non terminal file, order, unit, name, politic_order, troop_order, diplomatic_order, valid_order, erroneous_order;

/*
non terminal OrdersFile file;
non terminal HegemonyOrder order;
non terminal HegemonyOrder.Unit unit;
non terminal String name;
non terminal HegemonyOrder.OrderType order_type;
*/

start with file;

file ::= order:o EOL file:f					{: RESULT = new OrdersFile((OrdersFile) f, ((HegemonyOrder) o).setLine(((HegemonySymbol) CUP$parser$stack.elementAt(CUP$parser$top-1)).getLine())); :}
	   | order:o EOL						{: RESULT = new OrdersFile(((HegemonyOrder) o).setLine(((HegemonySymbol) CUP$parser$stack.peek()).getLine())); :}
       | EOL								/* do nothing */
       ;

order ::= valid_order | erroneous_order;

valid_order ::= unit:u name:s troop_order:a name:t	{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, (HegemonyOrder.Unit) u, (String) s, (String) t, ErrorType.VALID_ORDER); :} 
        | politic_order:a NUMBER:n name:t		{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, (String) t, (Integer) n, ErrorType.VALID_ORDER); :}
        | diplomatic_order:a name:t				{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, (String) t, ErrorType.VALID_ORDER); :}
        ;

name ::= WORD:w								{: RESULT = (String) w; :}
	   | WORD:w name:n						{: RESULT = ((String) w).concat(" ").concat((String) n); :}
	   ;
		
unit ::= ARMY 								{: RESULT = HegemonyOrder.Unit.ARMY; :}
       | NAVY 								{: RESULT = HegemonyOrder.Unit.NAVY; :}
       ;

troop_order ::= MOVE						{: RESULT = HegemonyOrder.OrderType.MOVE; :}
              | SUPPORT						{: RESULT = HegemonyOrder.OrderType.SUPPORT; :}
              ;

politic_order ::= POLITIC_POINTS			{: RESULT = HegemonyOrder.OrderType.POLITIC_POINTS; :}
                ;

diplomatic_order ::= DECLARE_WAR			{: RESULT = HegemonyOrder.OrderType.DECLARE_WAR; :}
                   | DECLARE_PEACE			{: RESULT = HegemonyOrder.OrderType.DECLARE_PEACE; :}
                   ;


/*** Typical errors ***/
erroneous_order ::= 
	
	  /* Politic order instead of troop order */
	  unit:u name:s politic_order:a name:t 		{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, (HegemonyOrder.Unit) u, (String) s, (String) t, ErrorType.WRONG_ORDER_TYPE); :} 
	  /* Diplomatic order instead of troop order */
	| unit:u name:s diplomatic_order:a name:t 	{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, (HegemonyOrder.Unit) u, (String) s, (String) t, ErrorType.WRONG_ORDER_TYPE); :} 
	  /* Troop order instead of politic order */
    | troop_order:a NUMBER:n name:t				{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, (String) t, (Integer) n, ErrorType.WRONG_ORDER_TYPE); :}
	  /* Diplomatic order instead of politic order */
    | diplomatic_order:a NUMBER:n name:t		{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, (String) t, (Integer) n, ErrorType.WRONG_ORDER_TYPE); :}
	  /* Troop order instead of diplomatic order */
    | troop_order:a name:t						{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, (String) t, ErrorType.WRONG_ORDER_TYPE); :}
	  /* Missing unit */
	| name:s troop_order:a name:t 				{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, null, (String) s, (String) t, ErrorType.MISSING_UNIT); :} 
	  /* Missing amount */
    | politic_order:a name:t					{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, (String) t, (Integer) 0, ErrorType.MISSING_AMOUNT); :}
	  /* Missing target */
	| unit:u name:s troop_order:a 				{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, (HegemonyOrder.Unit) u, (String) s, null, ErrorType.MISSING_TARGET); :} 
	  /* Missing target */
    | politic_order:a NUMBER:n					{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, null, (Integer) n, ErrorType.MISSING_TARGET); :}
	  /* Missing target */
    | diplomatic_order:a						{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, null, ErrorType.MISSING_TARGET); :}
	  /* Missing source */
	| unit:u troop_order:a name:t				{: RESULT = new HegemonyOrder((HegemonyOrder.OrderType) a, (HegemonyOrder.Unit) u, null, (String) t, ErrorType.MISSING_SOURCE); :}
	;
